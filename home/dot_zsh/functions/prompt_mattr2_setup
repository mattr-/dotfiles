# mattr prompt theme
# copied from the original (now deleted) mattr prompt theme and modified

autoload -U zgitinit
zgitinit

prompt_mattr2_help () {
  cat <<'EOF'

  prompt mattr2

EOF
}

# Borrow some git handling code from ohmyzsh, specifically to ensure that
# prompt git commands are read-only (TIL they aren't) and for hiding prompt
# info for larger git trees
function __git_prompt_git() {
  GIT_OPTIONAL_LOCKS=0 command git "$@"
}

function __git_prompt_info() {
  if ! __git_prompt_git rev-parse --git-dir &> /dev/null || \
     [[ "$(__git_prompt_git config --get mattr-.hide-info 2>/dev/null)" == 1 ]]; then
    return 0
  fi

  local ref
  ref=$(__git_prompt_git symbolic-ref --short HEAD 2>/dev/null) \
    || ref=$(__git_prompt_git rev-parse --short HEAD 2>/dev/null) \
    || return 0

  echo "at ${ref:gs/%/%%}"
}

function prompt_char2() {
  echo "  "
}

function prompt_mattr2_precmd() {
    local ex=$?
    psvar=()

    if [[ $ex -ge 128 ]]; then
        sig=$signals[$ex-127]
        psvar[1]="sig${(L)sig}"
    else
        psvar[1]="$ex"
    fi

    psvar[2]=$(prompt_char2)
    psvar[3]=$(__git_prompt_info)
}


prompt_mattr2_setup() {
    [[ "$USER" == "root" ]] && USERCOLOR="red" || USERCOLOR="cyan"
    user="$fg_bold[$USERCOLOR] %n$reset_color"
    waiting="%(?,%{$fg_bold[green]%}%2v,%{$fg_bold[red]%}%2v)${reset_color}"
    directory="%{$fg_no_bold[bright-white]%}  %4~${reset_color}"
    git_info="%{$fg_no_bold[green]%}%3v${reset_color}"

    PROMPT=
    #PROMPT+="%3v$fg[cyan]%n$fg[none]@$fg[bright-green]%m$fg[none]($fg[green]%5v$fg[none]%6v%7v$fg[none]:$fg[bright-white]%2~$fg[none])o%4v "
    PS1="$user$directory $git_info$prompt_newline$waiting"
    RPROMPT=
    precmd_functions+='prompt_mattr2_precmd'
}

prompt_mattr2_setup "$@"

# vim:set ft=zsh:
